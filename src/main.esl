// Copyright (c) 2009-2018, Brian G. Lucas.  See LICENSE file.

var debug:
    {	symb:
	    {   univ: boolean;
		prog: boolean;
		pack: boolean;
		proc: boolean;
	    };
	ast:   boolean;
	files: boolean;
	tests: [10]boolean;
    };
var feature:
    {   size:  (NO, YES, MIN);
	unused:boolean;		// warn if variable unused
	mflag: boolean;		// -M only generate Makefile stuff
	gflag: boolean;		// -g generate debug stuff
	trunc: boolean;		// warn on truncation
	tcall: boolean;		// indicate tail code in IR
	ambig: boolean;		// warn if assignment or call ambiguous
	assert:boolean;		// generate code for assert statements
	retv:  boolean;		// warn if old syntax for multiple return
	zerol: boolean;		// zero local scalar variables
	noinl: boolean;		// disable all inlining
	genptr:boolean;		// generate generic pointer "ptr"
	debug: _uint8;		// debug version
	version: _uint8;
    };

import sys;
import zstr;
import out;
const err: out.fildes = 2;      // error file descriptor for messages
const msg: out.fildes = 1;      // normal file descriptor for messages
alias	_byte as Char;
import lex;
alias   lex.Next as Next, lex.TokenT as TokenT, lex.Token as Token;
import type1;
alias	type.Type as Type, type.TypeT as TypeT, type.Valu as Valu;
import target;
alias	target.Target as Target;
import symb1;
alias   symb.SymbNode as type.SymbNode; // fixup forward reference
alias	symb.Symb as Symb, symb.SymbT as SymbT;
import ast;
alias   ast.AstNode as symb.AstNode;    // fixup forward reference
alias	ast.Ast as Ast, ast.Op as Op;
import symb2;
import expr1;
import type2;
import expr2;
import stmt;
import llvm;


proc main(argc:_uint, argv:@[]@[]_byte):_int //[[global]]
{
    var tree: Ast;
    var i: _uint;
    var outfd: sys.fildes;
    var outfile: @[]_byte;
    var targetarch: @[]_byte;
    var llvmversion: @[]_byte;

    outfile = 0;
    targetarch = 0;
    llvmversion = 0;
    outfd = 1;
    i = 1;
    while i < argc && argv[i]@[0] == '-' do
    {   if argv[i]@[1]
	is 'D' then
	{   if argv[i]@[2]
	    is 'a' then debug.ast = true;
	    is 's' then     // symbols except outermost
	    {   debug.symb.prog = true;
		debug.symb.pack = true;
		debug.symb.proc = true;
	    }
	    is 'S' then     // all symbols
	    {   debug.symb.univ = true;
		debug.symb.prog = true;
		debug.symb.pack = true;
		debug.symb.proc = true;
	    }
	    is 'f' then  debug.files = true;
	    is 't' then
	    {   if argv[i]@[3] >= '0' && argv[i]@[3] <= '9' then
		    debug.tests[argv[i]@[3] - '0'] = true;
	    }
	}
	is 'm' then
	{   if argv[i]@[2] != 0 then
		targetarch = argv[i]@[2:];
	    else
	    {   i += 1;
		targetarch = argv[i];
	    }
	}
	is 'I' then stmt.SearchPathAdd(argv[i]@[2:], false);
	is 'o' then
	{   if argv[i]@[2] != 0 then
		outfile = argv[i]@[2:];
	    else
	    {   i += 1;
		outfile = argv[i];
	    }
	}
	is 'O' then
	{   if argv[i]@[2]
	    is 's' then feature.size = YES;
	    is 'z' then feature.size = MIN;
	}
	is 'F' then
	{   if argv[i]@[2]
	    is 'u' then feature.unused = true;
	    is 't' then feature.trunc = true;
	    is 'c' then feature.tcall = true;
	    is 'a' then feature.ambig = true;
	    is 'r' then feature.retv = true;
	}
	is 'M' then feature.mflag = true;
	is 'g' then feature.gflag = true;
	is 'L' then
	{   if argv[i]@[2] != 0 then
		llvmversion = argv[i]@[2:];
	    else
	    {   i += 1;
		llvmversion = argv[i];
	    }
	}
	is 'A' then
	{   if argv[i]@[2]
	    is 'a' then feature.assert = true;
	    is 'z' then feature.zerol = true;
	    is 'i' then feature.noinl = true;
	}
	i += 1;
    }
    if i >= argc then
	return 3;
    if feature.mflag then
    {   var n: _uint;
	n = zstr.len(argv[i]@, lex.PathMax);
	if zstr.eq(argv[i]@[n-4:4], ".esl", lex.PathMax) then
	{   argv[i]@[n-4] = 0;		// nul terminate
	    msg.str(argv[i]@);
	    msg.str(".o: ");
	    argv[i]@[n-4] = '.';	// remove nul terminate
	}
    }
    // Check for LLVM backend version
    if llvmversion != 0 then
    {   feature.version = 0;
        var j: _uint = 0;
        while llvmversion[j] >= '0' && llvmversion[j] <= '9' do
	{   feature.version = (feature.version * 10) + (llvmversion[j]-'0');
	    j += 1;
	}
    }
    else feature.version = 9;	// assume some default version
    if feature.version < 4 then
    {   err.str("Unsupported LLVM backend version: ");
        err.uint8(feature.version);
	err.nl();
	return 2;
    }
    feature.debug = 6;			// assume the latest
    if feature.gflag && feature.debug < 5 then
    {   err.str("Debug information not supported for LLVM version: ");
        err.str(llvmversion);
	err.nl();
	feature.gflag = false;
    }
    if feature.version > 15 then
	feature.genptr = true;
    if !target.set(targetarch) then
    {   err.str("Unsupported target: ");
	if targetarch != 0 then
	    err.str(targetarch);
	else
	    err.str(target.DefaultTarget);
	err.nl();
	target.list();
	return 2;
    }
    if debug.files then stmt.SearchPathPrint();
    if !stmt.FileFind(argv[i]@, true) then
    {   err.str("Unable to open input file: ");
	err.str(argv[i]@);
	err.nl();
	return 2;
    }
    if outfile != 0 then
    {   outfd = sys.open(outfile, sys.O_CREAT|sys.O_TRUNC|sys.O_WRONLY, 0o640);
	if outfd < 0 then
	{   err.str("Unable to create output file: ");
	    err.str(outfile@);
	    err.nl();
	    return 2;
	}
    }
    symb.Init();
    ast.Init();
    symb.Push(0, 0);	// preserve universal level
    tree = stmt.Program(argv[i]@);
    if debug.symb.prog then
	symb.Dump(1);
    symb.Pop();
    if debug.symb.univ then
	symb.Dump(1);
    if debug.ast then
	ast.Dump(tree);
    type.FwdCheck();
    if lex.ErrorCount == 0 then
    {   if feature.mflag then
	    msg.nl();
	else
	    llvm.Gen(outfd, tree);
    }
    if lex.ErrorCount != 0 then
	return 1;
    return 0;
}
// vim: ts=8 sw=4 noet nowrap

