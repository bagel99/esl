// Copyright (c) 2009-2018, Brian G. Lucas.  See LICENSE file.

package symb
{
    type SymbT: (USYMB, CONSTANT, TYPE, VAR, FIELD, PROC, PACK, FILE, ALIAS, STRING);
    type LinkT: (FORMAL, LOCAL, NORMAL, GLOBAL, WEAK, EXTERN, FIXED, INLINE, NOINLINE);
    type OffsT: 0..0xFFFF;
    type IntrinsicT: ( NONE, DEF, ABS, MIN, MAX,
	CLZ, CLZNZ, CTZ, CTZNZ, POP,
	NEW, DEL, ZERO, ZLEN,
	PACKLE, PACKBE, UNPKLE, UNPKBE,
	ROL, ROR, BSWAP, BITREV, LO, HI, SPLICE,
	SQRT
    );
    type SymbNode:
    {
	next:	  @SymbNode;	// for lists
	type:	  Type;
	package:  @SymbNode;	// containing proc/package/file
	parmlist: @SymbNode;	// for procedures
	retvlist: @SymbNode;    // for procedures, returned value types
	symblist: @SymbNode;	// for packages, procedures, aliases
	altsymb:  @SymbNode;	// alternate name, e.g. external name
	section:  @SymbNode;    // linker section
	xaddr:    Valu;		// externally fixed address
	const:    @AstNode;	// for constants, initializer, default value
	addr:	  OffsT;	// e.g. field offset or intrinsic #
	lineno:   _uint16;	// line number where declared
	dbtag:    _uint16;	// tag for debug information
	kind:	  SymbT;	// TYPE, VAR, ...
	linkage:  LinkT;	// LOCAL, NORMAL, ...
	forward:  boolean;	// forward declared
	retv:     boolean;      // uses variables for return values
	bound:    boolean;	// is a bound procedure
	hasdef:   boolean;	// has default value
	ispad:    boolean;      // is a field used for padding
	used:	  boolean;	// was ever used (looked up)
	xname:    boolean;      // has characters LLVM needs escaped
	declared: boolean;	// used in llvm generation
	ident:	  []Char;
    };
    type Symb: @SymbNode;

    const NestMax = 31;
    type levels: 0..NestMax;
    var  level: levels;
    var  curfile: Symb;		// current file
    var  curpack: Symb;		// current package, 0 if not in a package
    var  curproc: Symb;		// current procedure, 0 if not in a procedure
    var  savpack: Symb;	// if we need to keep package context
    var  libnew, libnewnz, libdel: Symb;
    type symblist:
    {   frst:	Symb;
	last:	Symb;
	pkg:    Symb;		// package symbol or 0
    };
    var  symbs: [NestMax+1]symblist;
    type Visibility: (VISIBLE, DELAYED, PRIVATE);
    const anonid: lex.Identifier = "_";


    // Check for anonymous identifier
    proc IsAnon(id: @lex.Identifier): boolean
    {
	return zstr.eq(id, anonid, lex.IdentMax);
    }

    proc IsUTF8(id: @lex.Identifier): boolean
    {   var i: _uint;

	i = 0;
	while id[i] != 0 do
	{   if id[i] > 0x7F then return true;
	    i += 1;
        }
        return false;
    }

    // Check for uniqueness, fields, enum constants
    proc IsUniqueLocal(s: Symb, id: @lex.Identifier): boolean
    {
	if IsAnon(id) then return true;
	while s != 0 do
	{   if zstr.eq(s@.ident, id, lex.IdentMax) then return false;
	    s = s@.next;
	}
	return true;
    }

    proc FindThisLevel(name: @lex.Identifier): Symb
    {   var s: Symb;

	s = symbs[level].frst;
	while s != 0 && !zstr.eq(s@.ident, name, lex.IdentMax) do
	    s = s@.next;
        return s;
    }

    // Install symbol in symbol table
    proc MakeVisible(s: Symb)
    {
	if symbs[level].frst == 0 then
	    symbs[level].frst = s;
	else
	    symbs[level].last@.next = s;
	symbs[level].last = s;
    }

    proc New(kind: SymbT, name: @lex.Identifier, vis: Visibility): Symb
    {   var s: Symb;

	s = 0;
	if vis != PRIVATE then	// not private, see if already in use
	{   s = symbs[level].frst;
	    while s != 0 && !zstr.eq(s@.ident, name, lex.IdentMax) do
		s = s@.next;
	}
	if s != 0 then
	{   if s@.forward then
		return s;
	    else    // already defined, error message and make a new one
		lex.ErrorI(DEFID, name);
	}
	s = Symb(sys.malloc(SymbNode?size + zstr.len(name, lex.IdentMax) + 1));
	zstr.copy(s@.ident, name, lex.IdentMax);
	s@.xname = IsUTF8(name);
	s@.kind = kind;
	s@.next = 0;
	if curproc != 0 then
	{   s@.linkage = LOCAL;
	    s@.package = curproc;
	}
	else if curpack != 0 then
	{   s@.linkage = NORMAL;
	    s@.package = curpack;
	}
	else
	{   s@.linkage = GLOBAL;
	    s@.package = 0;
	}
	s@.lineno = lex.Lineno();
	if vis == VISIBLE then	// install in symbol table now
	    MakeVisible(s);
	return s;
    }

    alias lex.PathMax as PathMax;
    proc NewFile(path: @[]_byte, file: @[]_byte): Symb
    {   var fs: Symb;
	var full: [PathMax]Char;

	zstr.copy(full, path, PathMax);
	zstr.cat(full, "/", PathMax);
	zstr.cat(full, file, PathMax);
	fs = New(FILE, full, PRIVATE);
	fs@.package = curfile;
	curfile = fs;
	return fs;
    }

    proc NewString(str: @[]_byte, len: _uint, nopad: boolean): Symb
    {   var s: Symb;

	s = Symb(sys.malloc(SymbNode?size + len + 1));
	_zero(s);
	s@.ident[0:len] = str@;
	s@.ident[len] = 0;
	s@.kind = STRING;
	s@.ispad = nopad;
	s@.lineno = lex.Lineno();
	return s;
    }

    proc NewString2(str1: @[]_byte, len1: _uint,
		   str2: @[]_byte, len2: _uint, nopad: boolean): Symb
    {   var s: Symb;

	s = Symb(sys.malloc(SymbNode?size + len1 + len2 + 1));
	_zero(s);
	s@.ident[0:len1] = str1@;
	s@.ident[len1:len2] = str2@;
	s@.kind = STRING;
	s@.ispad = nopad;
	s@.lineno = lex.Lineno();
	return s;
    }

    proc Clone(os: Symb, name: @lex.Identifier): Symb
     {  var ns: Symb;

	ns = New(ALIAS, name, VISIBLE);
	ns@.symblist = os;
	return ns;
    }

    proc FindLocal(as: Symb, name: @lex.Identifier): Symb
    {   var s: Symb;

	s = as;
	while s != 0 do
	{   if zstr.eq(s@.ident, name, lex.IdentMax) then
	    {   if s@.kind == ALIAS then
		    s = s@.symblist;
		return s;
	    }
	    s = s@.next;
	}
	return s;
    }

    proc FindMethod(as: Symb, name: @lex.Identifier): Symb
    {   var s: Symb;

	s = as;
	while s != 0 && !zstr.eq(s@.ident, name, lex.IdentMax) do
	    s = s@.next;
	return s;
    }

    proc FindPackage(ps: Symb): Symb, Symb
    {   var s, fs: Symb;
	var tok: TokenT;
	var i: _uint;

	s = 0;
	tok = Token;
	if tok == ID then   // usually true
	    tok = Next();
	loop		    // loop thru package prefixes
	{ exit tok != DOT with lex.Expect(DOT);
	    tok = Next();
	  exit tok != ID with lex.Expect(ID);
	    fs = ps@.symblist;       // assume not a nested package
	    i = level;
	    while i > 0 && symbs[i].pkg != ps do
		i -= 1;
	    if i != 0 then	    // we are in the package we seek
		fs = symbs[i].frst;
	    s = FindLocal(fs, lex.Ident);
	  exit s == 0 || s@.kind != PACK;
	    tok = Next();
	    ps = s;
	}
	return s, ps;
    }

    proc FindTop(name: @lex.Identifier): Symb
    {   var s: Symb;
	var i: levels;

	i = level;
	loop
	{   s = FindLocal(symbs[i].frst, name);
	    if s != 0 then
	    {   s@.used = true;
		return s;
	    }
	  exit i == 0;
	    i -= 1;
	}
	return 0;
    }

    proc Find(name: @lex.Identifier): Symb
    {   var s: Symb;

	if savpack != 0 then
	{   s = FindLocal(savpack@.symblist, name);
	    savpack = 0;
	}
	else
	{   s = FindTop(name);
	    if s != 0 && s@.kind == PACK then
		s, _ = FindPackage(s);
	}
	return s;
    }

    proc FindKeep(name: @lex.Identifier): Symb
    {   var s, ps: Symb;

	s = FindTop(name);
	if s != 0 && s@.kind == PACK then
	{   s, ps = FindPackage(s);
	    savpack = ps;
	}
	return s;
    }

    proc ClearKeep()
    {
	savpack = 0;
    }

    proc FindKind(name: @lex.Identifier, kind:SymbT): Symb
    {   var s: Symb;

	s = Find(name);
	if s != 0 then
	{   if s@.kind != kind then
		if kind
		is TYPE then lex.Error(NOTTYPE);
		is VAR  then lex.Error(NOTVAR);
	}
	else lex.ErrorI(UNDEFID, name);
	return s;
    }

    const ImportSymbol = "_IMPORT";

    proc FindImport(name: @[]_byte): boolean
    {   var s: Symb;

//err.str("FindImport: "); err.str(name); err.nl();
	s = FindLocal(symbs[level].frst, ImportSymbol);
//err.str("\ts1="); err.uint(_uintptr(s)); err.nl();
	if s != 0 then
	    s = FindLocal(s.symblist, name);
//err.str("\ts2="); err.uint(_uintptr(s)); err.nl();
	return s != 0;
    }

    proc NewImport(name: @[]_byte): @[]_byte
    {   var s, t: Symb;

//err.str("NewImport: "); err.str(name); err.nl();
	s = FindLocal(symbs[level].frst, ImportSymbol);
	if s == 0 then
	    s = New(USYMB, ImportSymbol, VISIBLE);
	t = NewString(name, zstr.len(name, 256), false);
	t.next = s.symblist;
	s.symblist = t;
	return t.ident;
    }

    proc IsDefined(name: @lex.Identifier): boolean
    {   var s: Symb;
	var tok: TokenT;

	s = FindTop(name);
	loop
	{   tok = Next();
	  exit tok != DOT;
	    tok = Next();
	  exit tok != ID;
	    if s != 0 then
		s = FindLocal(s@.symblist, lex.Ident);
	}
	return s != 0;
    }

    proc Save(): Symb
    {
	return symbs[level].frst;
    }

    proc Append(s: Symb): Symb
    {
	if s != 0 then
	{   if symbs[level].frst == 0 then      // empty so far
		symbs[level].frst = s;
	    else
		symbs[level].last@.next = s;
	    while s@.next != 0 do
		s = s@.next;
	    symbs[level].last = s;
	}
	return s;
    }

    proc Push(init: Symb, ps: Symb): Symb
    {
	if level < NestMax then
	{   level += 1;
	    if init == 0 then
	    {
		symbs[level].frst = 0;
		symbs[level].last = 0;
	    }
	    else
	    {
		symbs[level].frst = init;
		while init@.next != 0 do
		    init = init@.next;
		symbs[level].last = init;
	    }
	    symbs[level].pkg = ps;
	}
	else
	{	// FIXME: error
	}
	return init;
    }

    proc Pop()
    {
	if level > 0 then
	{
	    if feature.unused && curproc != 0 then
	    {   var s: Symb;
		s = symbs[level].frst;
		while s != 0 do
		{   if !s@.used && s@.linkage != FORMAL then
		    {   err.str("In ");
			if curpack != 0 then
			{   err.str(curpack@.ident);
			    err.chr('.');
			}
			err.str(curproc@.ident);
			err.str(" unused: ");
			err.str(s@.ident);
			err.nl();
		    }
		    s = s@.next;
		}
	    }
	    level -= 1;
	}
	else
	{	// FIXME: error
	}
    }
}
// vim: ts=8 sw=4 noet nowrap

