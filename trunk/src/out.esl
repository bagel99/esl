// Copyright (c) 2009-2011, Brian G. Lucas.  See LICENSE file.
// $Id: out.esl 568 2011-05-23 21:28:40Z bgl $

// Unbuffered formatted Output
// Assumes target wordsize is 32-bits or more.

import sys;
import zstr;

package out
{
    type OutFD: sys.fildes;

    proc (o: OutFD) nl()
    {
	sys.write(o, "\n", 1);
    }

    proc (o: OutFD) chr(c: _byte)
    {   var buf: [1]_byte;

	buf[0] = c;
	sys.write(o, buf, 1);
    }

    proc (o: OutFD) str(s:@[]_byte)
    {   var n: _uint;

	n = zstr.len(s, 0xFFFF);
	sys.write(o, s, n);
    }

    proc (o: OutFD) strn(s:@[]_byte, len:_uint)
    {
	sys.write(o, s, len);
    }

    const hex: [16]_byte =
	{'0','1','2','3','4','5','6','7','8','9','A','B','C','D','E','F'};

    proc (o: OutFD) hex8(in: _uint8)
    {   var buf: [2]_byte;

	buf[0] = hex[(in>>4)&0xF];
	buf[1] = hex[in&0xF];
	sys.write(0, buf, 2);
    }

    proc (o: OutFD) hex16(in:_uint16)
    {   var buf: [4]_byte;

	buf[0] = hex[(in>>12)&0xF];
	buf[1] = hex[(in>>8)&0xF];
	buf[2] = hex[(in>>4)&0xF];
	buf[3] = hex[in&0xF];
	sys.write(o, buf, 4);
    }

    proc (o: OutFD) hex32(in:_uint32)
    {	var buf: [8]_byte;
	var i: _uint;

	i = 7;
	loop
	{   buf[i] = hex[in&0xF];
	  exit i == 0;
	    i -= 1;
	    in = in >> 4;
	}
	sys.write(o, buf, 8);
    }

    proc (o: OutFD) hex64(in: _uint64)
    {   var buf: [16]_byte;
	var i: 0..15;

	i = 15;
	loop
	{   buf[i] = hex[_uint(in)&0xF];
	  exit i == 0;
	    i -= 1;
	    in = in >> 4;
	}
	sys.write(o, buf, 16);
    }  

    proc(o: OutFD)  uint64(in: _uint64)
    {	var i, n: _uint;
	var buf: [20]_byte;

	i = 20;
	if in == 0 then
	{   i -= 1;
	    buf[i] = '0';
	}
	else
	{   while in != 0 do
	    {   i -= 1;
		buf[i] = _byte((in % 10) + '0');
		in = in / 10;
	    }
	}
	sys.write(o, buf[i:], 20-i);
    }

    proc(o: OutFD)  uint32(in: _uint32)
    {	var i, n: _uint;
	var buf: [10]_byte;

	if _uint?size == _uint64?size then  // extend to native
	    o.uint64(_uint64(in));
	else
	{   i = 10;
	    if in == 0 then
	    {   i -= 1;
		buf[i] = '0';
	    }
	    else
	    {   while in != 0 do
		{   i -= 1;
		    buf[i] = _byte((in % 10) + '0');
		    in = in / 10;
		}
	    }
	    sys.write(o, buf[i:], 10-i);
 	}
    }

    proc (o: OutFD) uint16(in: _uint16)
    {
	if _uint?size == _uint64?size then  // extend to native
	    o.uint64(_uint64(in));
	else
	    o.uint32(_uint32(in));
    }

    proc (o: OutFD) uint8(in: _uint8)
    {
	if _uint?size == _uint64?size then  // extend to native
	    o.uint64(_uint64(in));
	else
	    o.uint32(_uint32(in));
   }

    proc (o: OutFD) uint(in: _uint):: inline
    {
	if _uint?size == _uint64?size then
	    o.uint64(in);
	else
	    o.uint32(_uint32(in));
    }

    proc (o: OutFD) int64(in: _int64)
    {	var ui: _uint64;

	if in < 0 then
	{   sys.write(o, "-", 1);
	    ui = -in;
	}
	else
	    ui = in;
	o.uint64(ui);
    }	

    proc (o: OutFD) int32(in: _int32)
    {	var ui: _uint32;

	if _int?size == _int64?size then  // extend to native
	    o.int64(_int64(in));
	else
	{   if in < 0 then
	    {   sys.write(o, "-", 1);
		ui = -in;
	    }
	    else
		ui = in;
	    o.uint32(ui);
	}
    }	

    proc (o: OutFD) int16(in: _int16)
    {
	if _int?size == _int64?size then  // extend to native
	    o.int64(_int64(in));
	else
	    o.int32(_int32(in));
    }

    proc (o: OutFD) int8(in: _int8)
    {
	if _int?size == _int64?size then  // extend to native
	    o.int64(_int64(in));
	else
	    o.int32(_int32(in));
    }

    proc (o: OutFD) int(in: _int):: inline
    {
	if _int?size == _int64?size then
	    o.int64(in);
	else
	    o.int32(_int32(in));
    }
}
// vim: ts=8 sw=4 noet nowrap

