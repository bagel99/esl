// Copyright (c) 2009-2011, Brian G. Lucas.  See LICENSE file.
// $Id: main.esl 638 2011-10-23 20:47:16Z bgl $

var debug:
    {	symb:
	    {   univ: boolean;
		prog: boolean;
		pack: boolean;
		proc: boolean;
	    };
	ast:   boolean;
	files: boolean;
	tests: [10]boolean;
    };
var feature:
    {   size:  boolean;
	unused:boolean;
	mflag: boolean;
	gflag: boolean;
	trunc: boolean;
	tcall: boolean;
    };

import sys;
import zstr;
import out;
const err: sys.fildes = 2;      // error file descriptor for messages
const msg: sys.fildes = 1;      // normal file descriptor for messages
alias	_byte as Char;
import lex;
alias   lex.Next as Next, lex.TokenT as TokenT, lex.Token as Token;
import type1;
alias	type.Type as Type, type.TypeT as TypeT, type.Valu as Valu;
import target;
alias	target.Target as Target;
import symb1;
alias   symb.SymbNode as type.SymbNode; // fixup forward reference
alias	symb.Symb as Symb, symb.SymbT as SymbT;
import ast;
alias   ast.AstNode as symb.AstNode;    // fixup forward reference
alias	ast.Ast as Ast, ast.Op as Op;
import symb2;
import expr1;
import type2;
import expr2;
import stmt;
import llvm;


proc main(argc:_uint, argv:@[]@[]_byte):_int //[[global]]
{
    var tree: Ast;
    var i: _uint;
    var outfd: sys.fildes;
    var outfile: @[]_byte;
    var targetarch: @[]_byte;

    outfile = 0;
    targetarch = 0;
    outfd = 1;
    i = 1;
    while i < argc && argv[i]@[0] == '-' do
    {   if argv[i]@[1]
	is 'D' then 
	{   if argv[i]@[2]
	    is 'a' then debug.ast = true;
	    is 's' then 
	    {   // FIXME: allow individual selection
		debug.symb.univ = true;
		debug.symb.prog = true;
		debug.symb.pack = true;
		debug.symb.proc = true;
	    }
	    is 'f' then  debug.files = true;
	    is 't' then
	    {   if argv[i]@[3] >= '0' && argv[i]@[3] <= '9' then
		    debug.tests[argv[i]@[3] - '0'] = true;
	    }
	}
	is 'm' then 
	{   if argv[i]@[2] != 0 then
		targetarch = argv[i]@[2:];
	    else
	    {   i += 1;
		targetarch = argv[i];
	    }
	}
	is 'I' then lex.SearchPathAdd(argv[i]@[2:], false);
	is 'o' then
	{   if argv[i]@[2] != 0 then
		outfile = argv[i]@[2:];
	    else
	    {   i += 1;
		outfile = argv[i];
	    }
	}
	is 'O' then 
	{   if argv[i]@[2]
	    is 's' then feature.size = true;
	}
	is 'F' then 
	{   if argv[i]@[2]
	    is 'u' then feature.unused = true;
	    is 't' then feature.trunc = true;
	    is 'c' then feature.tcall = true;
	}
	is 'M' then feature.mflag = true;
	is 'g' then feature.gflag = true;
	i += 1;
    }
    if i >= argc then
	return 3;
    if feature.mflag then
    {   var n: _uint;
	n = zstr.len(argv[i]@, lex.PathMax);
	if zstr.eq(argv[i]@[n-4:4], ".esl", lex.PathMax) then
	{   argv[i]@[n-4] = 0;		// nul terminate
	    msg.str(argv[i]@);
	    msg.str(".o: ");
	    argv[i]@[n-4] = '.';	// remove nul terminate
	}
    }
    if !target.set(targetarch) then
    {   err.str("Unsupported target: ");
	if targetarch != 0 then
	    err.str(targetarch);
	else
	    err.str(target.DefaultTarget);
	err.nl();
	target.list();
	return 2;
    }
    if !lex.FileOpen(argv[i]@) then
    {   err.str("Unable to open input file: ");
	err.str(argv[i]@);
	err.nl();
	return 2;
    }
    if outfile != 0 then
    {   outfd = sys.open(outfile, sys.O_CREAT|sys.O_TRUNC|sys.O_WRONLY, 0o640);
	if outfd < 0 then
	{   err.str("Unable to create output file: ");
	    err.str(outfile@);
	    err.nl();
	    return 2;
	}
    }
    symb.Init();
    symb.Push(0);	// preserve universal level
    tree = stmt.Program(argv[i]@);
    if debug.symb.prog then
	symb.Dump(1);
    symb.Pop();
    if debug.symb.univ then
	symb.Dump(1);
    if debug.ast then
	ast.Dump(tree);
    if lex.ErrorCount == 0 then
    {   if feature.mflag then
	    msg.nl();
	else
	    llvm.Gen(outfd, tree);
    }
    if lex.ErrorCount != 0 then
	return 1;
    return 0;
}
// vim: ts=8 sw=4 noet nowrap

